If you wish to use GORM for Hibernate outside of a Grails application you should declare the necessary dependencies for GORM and the database you are using, for example in Gradle:

[source,groovy]
----
compile "org.grails:grails-datastore-gorm-hibernate4:VERSION"
compile "org.springframework:spring-expression:VERSION"
compile "org.springframework:spring-aop:VERSION"
compile "com.h2database:h2:VERSION'

----

Then annotate your entities with the `grails.gorm.annotation.Entity` annotation:

[source,groovy]
----
@Entity
class Person implements GormEntity<Person> {
    String firstName
    String lastName
    static constraints = {
        firstName blank:false
        lastName blank:false
    }
}
----

Then you need to place the bootstrap logic somewhere in your application which uses `HibernateDatastoreSpringInitializer`:

[source,groovy]
----
def initializer = new HibernateDatastoreSpringInitializer(Person)
def applicationContext = initializer.configureForDataSource(dataSource)

----

For configuration you can either pass a map or an instance of the `org.springframework.core.env.PropertyResolver` interface:

[source,groovy]
----
def initializer = new HibernateDatastoreSpringInitializer(['hibernate.log_sql':'true'], Person)
def applicationContext = initializer.configure()

----

If you are using Spring with an existing `ApplicationContext` you can instead call `configureForBeanDefinitionRegistry` prior to refreshing the context. You can pass the Spring `Environment` object to the constructor for configuration:

[source,groovy]
----
ApplicationContext myApplicationContext = ...
def initializer = new HibernateDatastoreSpringInitializer(myApplicationContext.getEnvironment(), Person)
initializer.configureForBeanDefinitionRegistry(myApplicationContext)

----

Note in this case GORM expects there to be a bean named `dataSource` present.

There are a couple of things Grails does automaticaly for you that you'll have to do it yourself.
You must define the `dataSource`, and you have to acquire a new Session via `withNewSession` for any GORM operation.

Below is a full example for using GORM in standalone application

[source,groovy]
----
def dataSource = new DriverManagerDataSource("jdbc:h2:prodDb;MVCC=TRUE;LOCK_TIMEOUT=10000;DB_CLOSE_ON_EXIT=FALSE", 'sa', '')
dataSource.setDriverClassName(org.h2.Driver.name)

def initializer = new HibernateDatastoreSpringInitializer(Person)
def applicationContext = initializer.configureForDataSource(dataSource)


Person.withNewSession {
    def person = new Person(firstName: 'John', lastName: 'Doe').save()
    println person.count()
}

@Entity
class Person implements GormEntity<Person> {
    String firstName
    String lastName
    static constraints = {
        firstName blank:false
        lastName blank:false
    }
}

----