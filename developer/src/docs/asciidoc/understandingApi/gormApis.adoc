By default the GORM compiler will make all GORM entities implement the `GormEntity` trait. Which provide all of the default GORM methods. However if you want to extend GORM to provide more methods specific to a given data store you can do so by extending this trait.

For example Neo4j adds methods for Cypher querying:

[source,groovy]
----
trait Neo4jEntity<D> extends GormEntity<D> {

    static Result cypherStatic(String queryString, Map params ) {
        def session = AbstractDatastore.retrieveSession(Neo4jDatastore)
        def graphDatabaseService = (GraphDatabaseService)session.nativeInterface
        graphDatabaseService.execute(queryString, params)
    }
}
----

With this addition then you then need to tell the GORM compiler to make entities implement this trait. To do that implement a `TraitProvider`:

[source,groovy]
----
package org.grails.datastore.gorm.neo4j

import grails.neo4j.Neo4jEntity
import groovy.transform.CompileStatic
import org.grails.compiler.gorm.GormEntityTraitProvider

@CompileStatic
class Neo4jEntityTraitProvider implements GormEntityTraitProvider {
    final Class entityTrait = Neo4jEntity
}
----

And then add a `src/main/resources/META-INF/services/org.grails.compiler.gorm.GormEntityTraitProvider` file specifying the name of your trait provider:

[source,groovy]
----
org.grails.datastore.gorm.neo4j.Neo4jEntityTraitProvider
----

GORM will automatically inject to trait into any domain class found in `grails-app/domain` or annotated with the `Entity` annotation, unless Hibernate is on the classpath in which case you have to tell GORM to map the domain class with Neo4j:

[source,groovy]
----
static mapWith = "neo4j"
----


