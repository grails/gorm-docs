Create, Read, Update and Delete (or CRUD) are the most common use cases when working with databases. The following examples will work you through how to perform each.

==== Create - Saving domain instances

To create and save a new instance to the database you should use the `save()` method:


[source,groovy]
----
new Book(title:"The Stand")
        .save()
        .toBlocking()
        .first()
----

The `save()` method returns an http://reactivex.io/RxJava/javadoc/rx/Observable.html[rx.Observable].

Notice that in this example we are calling `toBlocking()`, this is purely for demonstration and testing purposes to ensure the operation completes immediately.

In a real world application you would typically subscribe to the observable in order to receive a result:


[source,groovy]
----
new Book(title:"The Stand")
        .save()
        .subscribe { Book it ->
    println "Title = ${it.title}"
}
----

NOTE: If you don't call `subscribe(..)` or convert the `Observable` into a blocking operation, then no query will be executed! At least one call to `subscribe(..)` is required.

==== Read - Retrieving domain instances

To retrieve an instance you can use the static `get(..)` method:

[source,groovy]
----
Book.get(id)
    .subscribe { Book it ->
    println "Title = ${it.title}"
}
----

==== Update - Updating instances

To update an instance after retreiving it you can use the http://reactivex.io/RxJava/javadoc/rx/Observable.html#switchMap(rx.functions.Func1)[Observable.switchMap] method:

[source,groovy]
----
Book.get(id)
    .switchMap() { Book it ->
    it.title = "The Shining"
    it.save()
}.subscribe { Book updated ->
    println "Book updated!"
}
----

The `switchMap` transforms an `Observable` and converters the result into another `Observable`. However, this is not the most efficient way to perform updates as you can use http://docs.grails.org/latest/guide/GORM.html#whereQueries[where queries] to update an instance without retrieving it:


[source,groovy]
----
Book.where {
    id == id
}.updateAll(title:"The Shining")
 .subscribe { Number updateCount ->
    println "${updateCount} books were updated!"
}
----

==== Delete - Deleting instances

To delete an instance after retreiving it you can use the http://reactivex.io/RxJava/javadoc/rx/Observable.html#switchMap(rx.functions.Func1)[Observable.switchMap] method:

[source,groovy]
----
Book.get(id)
    .switchMap() { Book it ->
    it.delete()
}.subscribe { Boolean wasDeleted ->
    if(wasDeleted) {
        println "Book deleted!"
    }
}
----

But, once again it is more efficient with where queries:

[source,groovy]
----
Book.where {
    id == id
}.deleteAll()
 .subscribe { Number deleteCount ->
    println "${deleteCount} books were deleted!"
}
----


