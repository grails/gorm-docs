buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

ext {
	githubBranch = "master"
	checkOutDir = "build/checkout"
	zipFile = "build/source.zip"

	coreProjects = [
		'core',
		'simple',
		'web',
		'gorm',
		'gorm-plugin-support'
	]
}

version project.gormVersion

repositories {
	maven {
		url "https://repo.grails.org/grails/core"
	}
}
configurations {
	groovy
	documentation
}

dependencies {
	groovy "org.codehaus.groovy:groovy-all:2.4.6"
	for(p in coreProjects) {
		documentation "org.grails:grails-datastore-$p:$gormVersion"	
	}
	
}

task fetchSource << {
	ant.mkdir dir: project.buildDir
    ant.mkdir dir: checkOutDir

    println "Downloading GORM source code."
    def tag = System.getenv('TRAVIS_TAG')
    if(tag) {
        ant.get src: "https://github.com/grails/grails-data-mapping/archive/${tag}.zip", dest: zipFile, verbose: true
    }
    else {
        ant.get src: "http://github.com/grails/grails-data-mapping/zipball/${githubBranch}", dest: zipFile, verbose: true
    }
    
    ant.unzip src: zipFile, dest: checkOutDir, {
        mapper type: "regexp", from: "(grails-\\S*?/)(.*)", to: "gorm-src/\\2"
    }

    println "GORM source code downloaded."
}

fetchSource.inputs.properties(branch:githubBranch)
fetchSource.outputs.dir checkOutDir

task groovydoc(type:Groovydoc, dependsOn:fetchSource) {
	docTitle = "GORM $gormVersion"
	destinationDir = project.file("build/api")

	def files 
	for(p in coreProjects) {
		if(files == null) {
			files = project.files("${checkOutDir}/gorm-src/grails-datastore-${p}/src/main/groovy")
		}
		else {
			files += project.files("${checkOutDir}/gorm-src/grails-datastore-${p}/src/main/groovy")	
		}
	}
	source = files
	classpath = configurations.documentation
	groovyClasspath = configurations.groovy
}

subprojects { project ->
	apply plugin: 'org.asciidoctor.convert'
	asciidoctor {
	    resources {
	        from("${project.projectDir}/src/docs/images")
	        into "${project.projectDir}/images"
	    }

	    attributes 'experimental'  : 'true',
	               'compat-mode'   : 'true',
	               'toc'           : 'left',
	               'icons'         : 'font',
	               'version'       : project.version,
	               'sourcedir'     : "${project.projectDir}/src/main/groovy"
	}	
}